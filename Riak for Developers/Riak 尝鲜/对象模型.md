
# 对象模型

作为一名开发者，当发现 Bitcask 后端不支持二级索引(2i)时，你可能会感到惊慌失措。你可能想知道如何玩转数据“集合”。

在本节内容中，将对上面两个问题进行相应说明；在下一章中，我们将会介绍一些重要的分布式系统概念，例如 [siblings](http://docs.basho.com/riak/2.1.3/theory/concepts/Vector-Clocks/#Siblings)，[最终一致性](http://docs.basho.com/riak/2.1.3/theory/concepts/Eventual-Consistency/)，以及 [sibling resolution](http://docs.basho.com/riak/2.1.3/theory/concepts/Vector-Clocks/#Siblings)，以便将你打造成职业 Riak 开发者。

在本节的其余部分，我们将实现一个内部消息 app 的各部分代码。该应用代号取为 “Msgy” 。
简单来说，该 app 允许我们向彼此发送短小的文本片段（Msgs），而无需处理 email 格式中的繁文缛节。

在数据层面，我们仅需要处理如下几种对象：

- **User** — 用户对象，由 user_name ，full_name 和 email 字段构成；
- **Msg** — 用户所发送的消息对象，由正文 text ，发送者地址 sender ，接受者地址 recipient，以及创建日期 created 构成；
- **Timeline** — 消息列表，包含一个 owner 和一个 msg_type 。msg_type 可以为以下两种值：
  - **Inbox**: 用户个人消息的收件箱，或者群组公共消息列表；
  - **Sent**: 用户已发送消息列表；

Timeline 对象可以实现成基于 2i 的跨 Msg bucket 查询，但本节将展示如何仅使用 key/value 操作完成相应的功能。


在开始前，让我们先创建几个将要用到的记录。

> 代码下载
> 
> 本章相关代码可以在这里下载：[Github](https://github.com/basho/taste-of-riak/tree/master/erlang/Ch03-Msgy-Schema) 。    
> Github 上的代码包含了 Erlang 的 type specification ，此处为了简便省略掉了。    

```erlang
%% msgy.hrl

-define(USER_BUCKET, <<"Users">>).
-define(MSG_BUCKET, <<"Msgs">>).
-define(TIMELINE_BUCKET, <<"Timelines">>).
-define(INBOX, "Inbox").
-define(SENT, "Sent").

-record(user, {user_name, full_name, email}).
-record(msg, {sender, recipient, created, text}).
-record(timeline, {owner, msg_type, msgs}).
```

我们将使用名为 `Users` 的 bucket 保存我们的数据。在此，我们将不使用 [bucket types](http://docs.basho.com/riak/2.1.3/dev/advanced/bucket-types/) ，故不进行相应指定。

为了使用这些记录（结构）保存数据，首先需要创建一个 user 记录；    
当一个用户创建了一条消息，我们就需要将该消息 append 到一个或多个 timeline 中；    
若其为私有消息，则需 append 到 recipient 的收件箱 timeline 和用户自己的已发送 timeline 中；    
若其为群组消息，则需 append 到群组的 timeline 和用户自己的已发送 timeline 中；    

### 重温 Bucket 和 key

Now that we've worked out how we will differentiate data in the system, let's figure out our bucket and key names.

The bucket names are straightforward. We can use Users, Msgs, and Timelines. The key names, however, are a little more tricky. In past examples we've used sequential integers, but this presents a problem: we would need a secondary service to hand out these IDs. This service could easily be a future bottleneck in the system, so let's use a natural key. Natural keys are a great fit for key/value systems because both humans and computers can easily construct them when needed, and most of the time they can be made unique enough for a KV store.


| Bucket   | Key Pattern | Example Key
|---------|-------------|-------------
| Users    | &lt;user_name&gt; | joeuser
| Msgs     | &lt;username&gt;\_&lt;datetime&gt; | joeuser\_2014-03-06T02:05:13.223556Z
| Timelines | &lt;username&gt;\_&lt;type&gt;\_&lt;date&gt; | joeuser\_Sent\_2014-03-06Z
| | | marketing\_group\_Inbox\_2014-03-06Z

For the Users bucket, we can be certain that we will want each username to be unique, so let's use the username as the key. For the Msgs bucket, let's use a combination of the username and the posting datetime in an [ISO 8601](https://en.wikipedia.org/wiki/ISO_8601) Long format. This combination gives us the pattern &lt;username&gt;\_&lt;datetime&gt;, which produces keys like joeuser_2014-03-05T23:20:28Z.

Now for Timelines, we need to differentiate between Inbox and Sent timelines, so we can simply add that type into the key name. We will also want to partition each collection object into some time period, that way the object doesn't grow too large (see note below).

For Timelines, let's use the pattern &lt;username&gt;\_&lt;type&gt;\_&lt;date&gt; for users, and &lt;groupname&gt;\_Inbox\_&lt;date&gt; for groups, which will look like joeuser\_Sent\_2014-03-06Z or marketing\_group\_Inbox\_2014-03-05Z, respectively.



> 注意
> 
> Riak 的最佳性能将在存储对象的大小低于 1-2 MB 的情况下得以体现；    
> 若存储对象的大小超过 1-2 MB ，将会导致 Riak 的性能损耗，尤其存在许多 sibling 节点的情况下；    
> 下一章中，我们将着重介绍 silbing ，silbing resolution ，以及 sibling explosion 等问题。    


### Keeping our story straight with repositories

既然我们已经清楚了所需的对象模型，让我们编写一些作为仓库（repositories ）的模块，以帮助我们在 Riak 中创建和操作这些记录：

```erlang
%% user_repository.erl

-module(user_repository).
-export([save_user/2,
         get_user/2]).
-include("msgy.hrl").

save_user(ClientPid, User) ->
    RUser = riakc_obj:new(?USER_BUCKET,
                          list_to_binary(User#user.user_name),
                          User),
    riakc_pb_socket:put(ClientPid, RUser).

get_user(ClientPid, UserName) ->
    {ok, RUser} = riakc_pb_socket:get(ClientPid,
                                      ?USER_BUCKET,
                                      list_to_binary(UserName)),
    binary_to_term(riakc_obj:get_value(RUser)).
```

```erlang
%% msg_repository.erl

-module(msg_repository).
-export([create_msg/3,
         get_msg/2]).
-include("msgy.hrl").

-spec create_msg(user_name(), user_name(), text()) -> msg().
create_msg(Sender, Recipient, Text) ->
    #msg{sender=Sender,
         recipient=Recipient,
         created=get_current_iso_timestamp(),
         text = Text}.

-spec get_msg(pid(), riakc_obj:key()) -> msg().
get_msg(ClientPid, MsgKey) ->
    {ok, RMsg} = riakc_pb_socket:get(ClientPid,
                                     ?MSG_BUCKET,
                                     MsgKey),
    binary_to_term(riakc_obj:get_value(RMsg)).

%% @private
-spec get_current_iso_timestamp() -> datetimestamp().
get_current_iso_timestamp() ->
    {_,_,MicroSec} = DateTime = erlang:now(),
    {{Year,Month,Day},{Hour,Min,Sec}} = calendar:now_to_universal_time(DateTime),
    lists:flatten(
        io_lib:format("~4..0B-~2..0B-~2..0BT~2..0B:~2..0B:~2..0B.~6..0B",
            [Year, Month, Day, Hour, Min, Sec, MicroSec])).
```

```erlang
%% timeline_repository.erl

-module(timeline_repository).
-export([post_msg/2,
         get_timeline/4]).
-include("msgy.hrl").

post_msg(ClientPid, Msg) ->
     %% Save the canonical copy
    SavedMsg = save_msg(ClientPid, Msg),
    MsgKey = binary_to_list(riakc_obj:key(SavedMsg)),

    %% Post to sender's Sent timeline
    add_to_timeline(ClientPid, Msg, sent, MsgKey),

    %% Post to recipient's Inbox timeline
    add_to_timeline(ClientPid, Msg, inbox, MsgKey),
    ok.

get_timeline(ClientPid, Owner, MsgType, Date) ->
    TimelineKey = generate_key(Owner, MsgType, Date),
    {ok, RTimeline} = riakc_pb_socket:get(ClientPid,
                                          ?TIMELINE_BUCKET,
                                          list_to_binary(TimelineKey)),
    binary_to_term(riakc_obj:get_value(RTimeline)).

%% --------------------------------------------------------------------

%% @private
save_msg(ClientPid, Msg) ->
    MsgKey = Msg#msg.sender ++ "_" ++ Msg#msg.created,
    ExistingMsg = riakc_pb_socket:get(ClientPid,
                                      ?MSG_BUCKET,
                                      list_to_binary(MsgKey)),
    SavedMsg = case ExistingMsg of
        {error, notfound} ->
            NewMsg = riakc_obj:new(?MSG_BUCKET, list_to_binary(MsgKey), Msg),
            {ok, NewSaved} = riakc_pb_socket:put(ClientPid,
                                                 NewMsg,
                                                 [if_none_match, return_body]),
            NewSaved;
        {ok, Existing} -> Existing
    end,
    SavedMsg.

%% @private
add_to_timeline(ClientPid, Msg, MsgType, MsgKey) ->
    TimelineKey = generate_key_from_msg(Msg, MsgType),
    ExistingTimeline = riakc_pb_socket:get(ClientPid,
                                           ?TIMELINE_BUCKET,
                                           list_to_binary(TimelineKey)),
    UpdatedTimeline = case ExistingTimeline of
        {error, notfound} ->
            create_new_timeline(Msg, MsgType, MsgKey, TimelineKey);
        {ok, Existing} ->
            add_to_existing_timeline(Existing, MsgKey)
    end,

    {ok, SavedTimeline} = riakc_pb_socket:put(ClientPid,
                                              UpdatedTimeline,
                                              [return_body]),
    SavedTimeline.

%% @private
create_new_timeline(Msg, MsgType, MsgKey, TimelineKey) ->
    Owner = get_owner(Msg, MsgType),
    Timeline = #timeline{owner=Owner,
                         msg_type=MsgType,
                         msgs=[MsgKey]},
    riakc_obj:new(?TIMELINE_BUCKET, list_to_binary(TimelineKey), Timeline).

%% @private
add_to_existing_timeline(ExistingRiakObj, MsgKey) ->
    ExistingTimeline = binary_to_term(riakc_obj:get_value(ExistingRiakObj)),
    ExistingMsgList = ExistingTimeline#timeline.msgs,
    UpdatedTimeline = ExistingTimeline#timeline{msgs=[MsgKey|ExistingMsgList]},
    riakc_obj:update_value(ExistingRiakObj, UpdatedTimeline).

%% @private
get_owner(Msg, inbox) ->  Msg#msg.recipient;
get_owner(Msg, sent) ->  Msg#msg.sender.

%% @private
generate_key_from_msg(Msg, MsgType) ->
    Owner = get_owner(Msg, MsgType),
    generate_key(Owner, MsgType, Msg#msg.created).

%% @private
generate_key(Owner, MsgType, Date) when is_tuple(Date) ->
    DateString = get_iso_datestamp_from_date(Date),
    generate_key(Owner, MsgType, DateString);

generate_key(Owner, MsgType, Datetimestamp) ->
    DateString = get_iso_datestamp_from_iso_timestamp(Datetimestamp),
    MsgTypeString = case MsgType of
        inbox -> ?INBOX;
        sent -> ?SENT
    end,
    Owner ++ "_" ++ MsgTypeString ++ "_" ++ DateString.

%% @private
get_iso_datestamp_from_date(Date) ->
    {Year,Month,Day} = Date,
    lists:flatten(io_lib:format("~4..0B-~2..0B-~2..0B", [Year, Month, Day])).

%% @private
get_iso_datestamp_from_iso_timestamp(CreatedString) ->
    {Date, _} = lists:split(10,CreatedString),
    Date.
```

最后，让我们进行功能测试

```erlang
%% msgy.erl

-module(msgy).
-export([main/0]).
-include("msgy.hrl").

main() ->
  %% Setup our repositories
  {ok, Pid} = riakc_pb_socket:start_link("127.0.0.1", 10017),

  %% Create and save users
  Joe = #user{user_name="joeuser",
              full_name="Joe User",
              email="joe.user@basho.com"},

  Marleen = #user{user_name="marleenmgr",
                  full_name="Marleen Manager",
                  email="marleen.manager@basho.com"},

  user_repository:save_user(Pid, Joe),
  user_repository:save_user(Pid, Marleen),

  %% Create new Msg, post to timelines
  Msg = msg_repository:create_msg(Marleen#user.user_name, Joe#user.user_name, "Welcome to the company!"),
  timeline_repository:post_msg(Pid, Msg),


  %% Get Joe's inbox for today, get first message
  {TodaysDate,_} = calendar:now_to_universal_time(erlang:now()),
  JoesInboxToday = timeline_repository:get_timeline(Pid, Joe#user.user_name, inbox, TodaysDate),

  JoesFirstMessage = msg_repository:get_msg(Pid, hd(JoesInboxToday#timeline.msgs)),

  io:format("From: ~s~nMsg : ~s~n~n", [JoesFirstMessage#msg.sender, JoesFirstMessage#msg.text]),
  ok.
```

正如你所看到的，仓库模式（repository pattern）能够帮助我们解决以下一些问题：

- 在创建一个新对象前，确认其是否已经存在；
- 维护 bucket 和 key 名字的一致性
- 能够对外提供一致的接口

尽管上述各种仓库解决了我们的诸多问题，但是其设计仍旧简陋，并没有覆盖到所有边界情况。例如，当两个不同的人使用相同的用户名创建用户时会发生什么？

现在，我们可以轻易的“计算”出 key 名字了，但是如何快速查询某个用户最后发送的 10 条消息呢？许多这类问题的答案都是应用相关的。
例如，如果你的应用需要逆序展示最后 10 条消息，你可能需要将相应的数据集保存到另外的聚合对象中，以便提供更快速的查询；
要记住，每一种解决方案都存在某种不足，但我们还是推荐优先探索基于 key/valve 的方案，因为该方案可能是速度最快的；

回顾一下本章学到的内容：

- 如何选取 bucket 名字
- 如何基于数据分区设计目标选取 natural key ；